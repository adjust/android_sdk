apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/staging/"
    }
}

android {
    compileSdkVersion rootProject.ext.coreCompileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.webbridgeMinSdkVersion
        targetSdkVersion rootProject.ext.coreTargetSdkVersion
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Add SDK via module.
    compileOnly project(':sdk-core')
    // Add SDK via Maven.
    // implementation 'com.adjust.sdk:adjust-android:4.35.0'
}

// read local properties
File localPropsFile = project.rootProject.file('local.properties')
if (localPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(localPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
}

task adjustWebBridgeAndroidAar (type: Copy) {
    dependsOn 'assembleRelease'

    from('build/outputs/aar/')
    include 'sdk-webbridge-release.aar'
    destinationDir file('build/libs/')
    rename 'sdk-webbridge-release.aar', "${project.name}.aar"

}

task adjustWebBridgeAndroidJar(type: Jar) {
    dependsOn 'packageReleaseAssets'
    dependsOn 'compileReleaseJavaWithJavac'

    from('build/intermediates/library_assets/release/out/') {
        into('assets')
    }
    from('build/intermediates/javac/release/classes/')

    archiveName "${project.name}.jar"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            classpath += variant.javaCompileProvider.get().classpath
        }
    }
}

task adjustWebBridgeAndroidJavadocsJar(type: Jar) {
    dependsOn 'androidJavadocs'
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task adjustWebBridgeAndroidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives adjustWebBridgeAndroidJar
    archives adjustWebBridgeAndroidJavadocsJar
    archives adjustWebBridgeAndroidSourcesJar
}

publishing {
    publications {
//        mavenAndroidWebBridgeJar(MavenPublication) {
//            customizePomForJar(pom)
//            groupId rootProject.ext.adjustGroupId
//            artifactId 'adjust-android-webbridge'
//            version rootProject.ext.coreVersionName
//
//            artifact adjustWebBridgeAndroidJar
//            artifact adjustWebBridgeAndroidJavadocsJar
//            artifact adjustWebBridgeAndroidSourcesJar
//        }

        mavenAndroidWebBridgeAar(MavenPublication) {
            customizePomForAar(pom)
            groupId rootProject.ext.adjustGroupId
            artifactId 'adjust-android-webbridge'
            version rootProject.ext.coreVersionName

            artifact("$buildDir/outputs/aar/sdk-plugin-webbridge-release.aar"){
                builtBy tasks.getByPath("adjustWebBridgeAndroidAar")
            }
            artifact adjustWebBridgeAndroidJavadocsJar
            artifact adjustWebBridgeAndroidSourcesJar
        }
    }

    repositories {
        maven {
            if (rootProject.ext.coreVersionName.toString().contains('SNAPSHOT')) {
                println "Using Maven SNAPSHOT repository for publishing"
                url "https://oss.sonatype.org/content/repositories/snapshots/"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }
            if (project.hasProperty("sonatypeUsername")) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

def customizePomForJar(pom) {
    pom.withXml {
        def root = asNode()

        // Add all items necessary for maven central publication.
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The WebBridge plugin for Adjust SDK for Android'
            name 'Adjust Android SDK WebBridge plugin'
            url 'https://github.com/adjust/android_sdk'

            organization {
                name 'adjust GmbH'
                url 'http://www.adjust.com'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:adjust/android_sdk.git'
                connection 'scm:git:git@github.com:adjust/android_sdk.git'
                developerConnection 'scm:git:git@github.com:adjust/android_sdk.git'
            }
            developers {
                developer {
                    name 'Pedro Silva'
                    email 'pedro@adjust.com'
                }
                developer {
                    name 'Ugljesa Erceg'
                    email 'ugljesa@adjust.com'
                }
            }
        }
    }
}

def customizePomForAar(pom) {
    pom.withXml {
        // Remove the existing packaging field, as it has to be overridden with type aar
        asNode().remove(asNode().get("packaging"))

        // Add all items necessary for maven central publication.
        asNode().children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The WebBridge plugin for Adjust SDK for Android'
            name 'Adjust Android SDK WebBridge plugin'
            url 'https://github.com/adjust/android_sdk'
            packaging 'aar'
            organization {
                name 'adjust GmbH'
                url 'http://www.adjust.com'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:adjust/android_sdk.git'
                connection 'scm:git:git@github.com:adjust/android_sdk.git'
                developerConnection 'scm:git:git@github.com:adjust/android_sdk.git'
            }
            developers {
                developer {
                    name 'Pedro Silva'
                    email 'pedro@adjust.com'
                }
                developer {
                    name 'Ugljesa Erceg'
                    email 'ugljesa@adjust.com'
                }
            }
        }
    }
}

model {
//    tasks.generatePomFileForMavenAndroidWebBridgeJarPublication {
//        destination = file("${project.buildDir}/generated-pom.xml")
//    }
    /* TODO check if/how to replace this tasks
    tasks.publishMavenAndroidWebBridgePublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenAndroidWebBridgePublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
    */
    tasks.generatePomFileForMavenAndroidWebBridgeAarPublication {
        destination = file("${project.buildDir}/generated-pom.xml")
    }
}

signing {
//    sign publishing.publications.mavenAndroidWebBridgeJar
    sign publishing.publications.mavenAndroidWebBridgeAar
}
