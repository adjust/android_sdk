apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion rootProject.ext.coreCompileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.coreMinSdkVersion
        targetSdkVersion rootProject.ext.coreTargetSdkVersion
        versionName rootProject.ext.coreVersionName
        versionCode rootProject.ext.defaultVersionCode
        consumerProguardFiles 'adjust-proguard-rules.txt'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

task adjustCoreAndroidAar (type: Copy) {
    dependsOn 'assembleRelease'

    from('build/outputs/aar/')
    include 'sdk-core-release.aar'
    destinationDir file('build/libs/')
    rename 'sdk-core-release.aar', "${project.name}.aar"

}


task adjustSdkNonNativeAarDebug (type: Copy) {
    dependsOn 'assembleDebug'

    from ('build/outputs/aar/')
    include 'sdk-core-debug.aar'
    destinationDir file('build/libs/')
    rename 'sdk-core-debug.aar', "adjust-sdk-debug.aar"
}

task adjustSdkNonNativeAarRelease (type: Copy) {
    dependsOn 'assembleRelease'

    from ('build/outputs/aar/')
    include 'sdk-core-release.aar'
    destinationDir file('build/libs/')
    rename 'sdk-core-release.aar', "adjust-sdk-release.aar"
}

task adjustCoreAndroidJar(type: Jar) {
    dependsOn 'compileReleaseJavaWithJavac'
    from('build/intermediates/javac/release/compileReleaseJavaWithJavac/classes/')
    archiveName "${project.name}.jar"
}

task adjustSdkNonNativeJarDebug(type: Jar) {
    dependsOn 'compileDebugJavaWithJavac'
    from('build/intermediates/javac/debug/compileDebugJavaWithJavac/classes/')
    archiveName "adjust-sdk-debug.jar"
}

task adjustSdkNonNativeJarRelease(type: Jar) {
    dependsOn 'compileReleaseJavaWithJavac'
    from('build/intermediates/javac/release/compileReleaseJavaWithJavac/classes/')
    archiveName "adjust-sdk-release.jar"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // JDK 1.8 is more strict then 1.7. Have JDK 1.8 behave like 1.7 for javadoc generation.
    if (org.gradle.internal.jvm.Jvm.current().getJavaVersion() == JavaVersion.VERSION_1_8) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task adjustCoreAndroidJavadocsJar(type: Jar) {
    dependsOn 'androidJavadocs'
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task adjustCoreAndroidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives adjustCoreAndroidJar
    archives adjustCoreAndroidJavadocsJar
    archives adjustCoreAndroidSourcesJar
}

publishing {
    publications {
        mavenAndroidCore(MavenPublication) {
            customizePom(pom)
            groupId rootProject.ext.adjustGroupId
            artifactId 'adjust-android'
            version rootProject.ext.coreVersionName

            // Create the signed POM artifact.
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            // Create the signed artifacts.
            project.tasks.signArchives.signatureFiles.each {
                // Exclude "usual" archive artifact .aar.
                def signFileName = it.toString()
                if (signFileName.contains('aar')) {
                    return
                }
                // Create a Maven artifact for each asc signature.
                artifact(it) {
                    //println "archive name ${signFileName}"
                    if (signFileName.contains('-sources')) {
                        classifier = 'sources'
                        extension = 'jar.asc'
                    } else if (signFileName.contains('-javadoc')) {
                        classifier = 'javadoc'
                        extension = 'jar.asc'
                    } else {
                        classifier = null
                        extension = 'jar.asc'
                    }
                }
            }

            artifact adjustCoreAndroidJar
            artifact adjustCoreAndroidJavadocsJar
            artifact adjustCoreAndroidSourcesJar
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            if (project.hasProperty("sonatypeUsername")) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        // Add all items necessary for maven central publication.
        asNode().children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The Adjust SDK for Android'
            name 'Adjust Android SDK'
            url 'https://github.com/adjust/android_sdk'

            organization {
                name 'adjust GmbH'
                url 'http://www.adjust.com'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:adjust/android_sdk.git'
                connection 'scm:git:git@github.com:adjust/android_sdk.git'
                developerConnection 'scm:git:git@github.com:adjust/android_sdk.git'
            }
            developers {
                developer {
                    name 'Pedro Silva'
                    email 'pedro@adjust.com'
                }
                developer {
                    name 'Ugljesa Erceg'
                    email 'ugljesa@adjust.com'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenAndroidCorePublication {
        destination = file("${project.buildDir}/generated-pom.xml")
    }
    tasks.publishMavenAndroidCorePublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenAndroidCorePublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

signing {
    sign configurations.archives
}
