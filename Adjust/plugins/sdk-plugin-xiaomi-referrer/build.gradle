apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    maven {
        url "https://central.sonatype.com/repository/maven-snapshots/"
    }
}

android {
    compileSdk rootProject.ext.coreCompileSdkVersion

    publishing {
        multipleVariants {
            allVariants()
            withJavadocJar()
        }
    }

    defaultConfig {
        minSdkVersion rootProject.ext.coreMinSdkVersion
        targetSdkVersion rootProject.ext.coreTargetSdkVersion
    }
    namespace 'com.adjust.sdk.xiaomi'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Add SDK via module.
    compileOnly project(':sdk-core')
    // Add SDK via Maven.
    // implementation 'com.adjust.sdk:adjust-android:5.4.6'

    // Add xiaomi referrer lib via Maven.
    implementation 'com.miui.referrer:homereferrer:1.0.0.6'
}

// read local properties
File localPropsFile = project.rootProject.file('local.properties')
if (localPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(localPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
}

task adjustXiaomiReferrerPluginAar(type: Copy) {
    dependsOn 'assembleRelease'

    from('build/outputs/aar/')
    include 'sdk-plugin-xiaomi-referrer-release.aar'
    destinationDir file('build/libs/')
    rename 'sdk-plugin-xiaomi-referrer-release.aar', "${project.name}.aar"

}

task adjustXiaomiReferrerPluginJar(type: Jar) {
    dependsOn 'packageReleaseAssets'
    dependsOn 'compileReleaseJavaWithJavac'

    from('build/intermediates/library_assets/release/packageReleaseAssets/out/') {
        into('assets')
    }
    from('build/intermediates/javac/release/classes/')

    archiveFileName = project.provider { "${project.name}.jar" }
}

task adjustXiaomiReferrerPluginSourcesJar(type: Jar) {
    setArchiveClassifier('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives adjustXiaomiReferrerPluginJar
    archives adjustXiaomiReferrerPluginSourcesJar
}
afterEvaluate {
    publishing {
        publications {
/*
            mavenAndroidXiaomiReferrerJar(MavenPublication) {
                customizePomForJar(pom)
                groupId rootProject.ext.adjustGroupId
                artifactId 'adjust-android-xiaomi-referrer'
                version rootProject.ext.coreVersionName

                artifact adjustXiaomiReferrerPluginJar
                artifact adjustXiaomiReferrerPluginSourcesJar
            }
*/
            mavenAndroidXiaomiReferrerAar(MavenPublication) {
                customizePomForAar(pom)
                groupId rootProject.ext.adjustGroupId
                artifactId 'adjust-android-xiaomi-referrer'
                version rootProject.ext.coreVersionName

                artifact("$buildDir/outputs/aar/sdk-plugin-xiaomi-referrer-release.aar") {
                    builtBy tasks.getByPath("adjustXiaomiReferrerPluginAar")
                }
                artifact adjustXiaomiReferrerPluginSourcesJar
            }
        }

        repositories {
            maven {
                if (rootProject.ext.coreVersionName.toString().contains('SNAPSHOT')) {
                    println "Using Maven SNAPSHOT repository for publishing"
                    url "https://central.sonatype.com/repository/maven-snapshots/"
                } else {
                    url "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2"
                }
                if (project.hasProperty("sonatypeUsername")) {
                    credentials {
                        username sonatypeUsername
                        password sonatypePassword
                    }
                }
            }
        }
    }
    signing {
//        sign publishing.publications.mavenAndroidXiaomiReferrerJar
        sign publishing.publications.mavenAndroidXiaomiReferrerAar
    }
}

def customizePomForJar(pom) {
    pom.withXml {
        def root = asNode()

        // Add all items necessary for maven central publication.
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The Xiaomi Referrer plugin for Adjust SDK for Android'
            name 'Adjust Android SDK Xiaomi Referrer plugin'
            url 'https://github.com/adjust/android_sdk'

            organization {
                name 'adjust GmbH'
                url 'http://www.adjust.com'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:adjust/android_sdk.git'
                connection 'scm:git:git@github.com:adjust/android_sdk.git'
                developerConnection 'scm:git:git@github.com:adjust/android_sdk.git'
            }
            developers {
                developer {
                    name 'Pedro Silva'
                    email 'pedro@adjust.com'
                }
                developer {
                    name 'Ugljesa Erceg'
                    email 'ugljesa@adjust.com'
                }
            }
        }
    }
}

def customizePomForAar(pom) {
    pom.withXml {
        def root = asNode()

        // Add all items necessary for maven central publication.
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The Xiaomi Referrer plugin for Adjust SDK for Android'
            name 'Adjust Android SDK Xiaomi Referrer plugin'
            url 'https://github.com/adjust/android_sdk'

            organization {
                name 'adjust GmbH'
                url 'http://www.adjust.com'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:adjust/android_sdk.git'
                connection 'scm:git:git@github.com:adjust/android_sdk.git'
                developerConnection 'scm:git:git@github.com:adjust/android_sdk.git'
            }
            developers {
                developer {
                    name 'Pedro Silva'
                    email 'pedro@adjust.com'
                }
                developer {
                    name 'Ugljesa Erceg'
                    email 'ugljesa@adjust.com'
                }
            }
        }
    }
}

model {
//    tasks.generatePomFileForMavenAndroidXiaomiReferrerJarPublication {
//        destination = file("${project.buildDir}/generated-pom.xml")
//    }
    /* TODO check if/how to replace this tasks
    tasks.publishMavenAndroidXiaomiReferrerPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenAndroidXiaomiReferrerPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
    */
    tasks.generatePomFileForMavenAndroidXiaomiReferrerAarPublication {
        destination = file("${project.buildDir}/generated-pom.xml")
    }
}


